options {
    STATIC = false;
    SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
}

PARSER_BEGIN(JSON)

import java.io.StringReader;
import java.io.Reader;
import java.util.Map;
import java.util.Arrays;
import java.util.HashMap;

public class JSON {

    public static void main(String args[]) {
        try
        {
              JSON json = new JSON(System.in);
              Map<String,Object> map = json.parseObject();
			  System.out.println(Arrays.toString(map.entrySet().toArray()));
        } catch(Exception e) {
              e.printStackTrace();
        }
    }
}

PARSER_END(JSON)

SKIP :
{
      " " 
    | "\t" 
    | "\r" 
    | "\n"
}

TOKEN :
{
      <LBRACE: "{">
    | <RBRACE: "}">
    | <COLON: ":">
    | <COMMA: ",">
    | <TRUE: "true">
    | <FALSE: "false">
    | <NULL: "null">
    | <NUMBER: (["0"-"9"])+>
    | <STRING: "\"" (~["\""])* "\"">
}

Map<String,Object> parseObject() :
{
    Map<String,Object> obj;
}
{
    obj = object() <EOF>
	{ return obj; }
}

Map<String,Object> object() :
{
    Map<String,Object> obj = new HashMap<String,Object>();
}
{
    <LBRACE> [ member(obj) ( <COMMA> member(obj) )*  ] <RBRACE>
    { return obj; }
}

void member(Map<String,Object> obj) :
{
    Token key;
    Object value;
}
{
    key = <STRING> <COLON> value = value()
    { obj.put(key.image, value); } 
}

Object value() :
{
    Object value;
}
{
    ( value = nil() 
    | value = bool()
    | value = number()
    | value = string()
    | value = object() 
    )
    { return value; }
}

String string() :
{
    Token t;
}
{
    t = <STRING>
    { return t.image; }
}

Boolean bool() :
{
}
{
    ( <TRUE> 
      { return Boolean.TRUE; }
    | <FALSE>
      { return Boolean.FALSE; }
    )
}

Object nil() :
{
}
{
    <NULL>
    { return null; }
}

Number number() :
{
    Token t;
}
{
    t = <NUMBER>
    { return Integer.valueOf(t.image); }
}
